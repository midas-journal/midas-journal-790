PROJECT(BSplines)

CMAKE_MINIMUM_REQUIRED(VERSION 2.4)
IF(COMMAND CMAKE_POLICY)
  CMAKE_POLICY(SET CMP0003 NEW)
ENDIF(COMMAND CMAKE_POLICY)

ENABLE_TESTING()

SET(LIB_NAME vtk${PROJECT_NAME})
SET(LIB_VTK_LIBS vtkRendering vtkImaging vtkHybrid)
SET(LIB_SRCS
  vtkImageBSplineCoefficients.cxx
  vtkImageBSplineReslice.cxx
  vtkBSplineTransform.cxx
)

SET(TEST_NAME TestBSplineWarp)
SET(TEST_SRCS TestBSplineWarp.cxx)
SET(TEST_LIBS ${LIB_NAME})

SET(PYTHON_FILES
  TestBSplineWarp.py
)

# The remainder of this file can usually be left alone

FIND_PACKAGE(VTK REQUIRED)
INCLUDE(${VTK_USE_FILE})
IF(VTK_USE_CARBON)
SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -arch i386")
SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -arch i386")
ENDIF(VTK_USE_CARBON)

ADD_LIBRARY(${LIB_NAME} STATIC ${LIB_SRCS})
TARGET_LINK_LIBRARIES(${LIB_NAME} ${LIB_VTK_LIBS})

# Need to include class headers and the configuration header.
INCLUDE_DIRECTORIES(${PROJECT_SOURCE_DIR})
INCLUDE_DIRECTORIES(${PROJECT_BINARY_DIR})

STRING(TOUPPER ${PROJECT_NAME} PROJECT_NAME_UPPER)
STRING(TOLOWER ${PROJECT_NAME} PROJECT_NAME_LOWER)

# Build static, but use shared-library flags
IF(CMAKE_SHARED_LIBRARY_C_FLAGS)
SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES
                      COMPILE_FLAGS ${CMAKE_SHARED_LIBRARY_C_FLAGS})
ENDIF(CMAKE_SHARED_LIBRARY_C_FLAGS)

# Create the header to configure the classes.
SET(VTKLOCAL_BUILD_SHARED_LIBS_CONFIG 0)
CONFIGURE_FILE(${PROJECT_SOURCE_DIR}/vtkLocalConfigure.h.in
               ${PROJECT_BINARY_DIR}/${LIB_NAME}Configure.h
               @ONLY IMMEDIATE)

# Add tests
IF(TEST_NAME)
  ADD_EXECUTABLE(${TEST_NAME} ${TEST_SRCS})
  TARGET_LINK_LIBRARIES(${TEST_NAME} ${TEST_LIBS})
  ADD_TEST(${TEST_NAME} ${TEST_NAME} -t)
ENDIF(TEST_NAME)

# Use wrapping hints for this project.
SET(VTK_WRAP_HINTS "${PROJECT_SOURCE_DIR}/hints")

# Create Python wrappers if VTK is wrapped in Python.
IF(VTK_WRAP_PYTHON)
  CONFIGURE_FILE(${PROJECT_SOURCE_DIR}/vtkLocal.py.in
                 ${PROJECT_BINARY_DIR}/${PROJECT_NAME_LOWER}.py
                 @ONLY IMMEDIATE)
  IF(VTK_PYTHON_INCLUDE_DIR)
    SET(PYTHON_INCLUDE_DIR ${VTK_PYTHON_INCLUDE_DIR})
  ENDIF(VTK_PYTHON_INCLUDE_DIR)
  IF(VTK_PYTHON_LIBRARY)
    SET(PYTHON_LIBRARIES ${VTK_PYTHON_LIBRARY})
  ENDIF(VTK_PYTHON_LIBRARY)
  SET(VTK_WRAP_PYTHON_FIND_LIBS ON)
  INCLUDE(${VTK_CMAKE_DIR}/vtkWrapPython.cmake)
  INCLUDE_DIRECTORIES("${PYTHON_INCLUDE_PATH}")
  VTK_WRAP_PYTHON3(${LIB_NAME}Python vtkLocalPYTHON_SRCS "${LIB_SRCS}")
  ADD_LIBRARY(${LIB_NAME}Python MODULE ${vtkLocalPYTHON_SRCS})
  FOREACH(c ${LIB_VTK_LIBS})
    TARGET_LINK_LIBRARIES(${LIB_NAME}Python ${c}PythonD)
  ENDFOREACH(c)
  TARGET_LINK_LIBRARIES(${LIB_NAME}Python ${LIB_NAME})
  IF(${VTK_MAJOR_VERSION} GREATER 5 OR
     ${VTK_MAJOR_VERSION} EQUAL 5 AND ${VTK_MINOR_VERSION} GREATER 6)
    SET_TARGET_PROPERTIES(${LIB_NAME}Python PROPERTIES PREFIX "")
  ENDIF(${VTK_MAJOR_VERSION} GREATER 5 OR
       ${VTK_MAJOR_VERSION} EQUAL 5 AND ${VTK_MINOR_VERSION} GREATER 6)
  IF(WIN32)
    SET_TARGET_PROPERTIES(${LIB_NAME}Python PROPERTIES SUFFIX ".pyd")
  ENDIF(WIN32)
  IF (NOT "${PROJECT_BINARY_DIR}" MATCHES "^${PROJECT_SOURCE_DIR}$")
    FOREACH(file ${PYTHON_FILES})
      SET(src "${PROJECT_SOURCE_DIR}/${file}")
      SET(tgt "${PROJECT_BINARY_DIR}/${file}")
      CONFIGURE_FILE(${src} ${tgt} COPYONLY)
    ENDFOREACH(file)
  ENDIF (NOT "${PROJECT_BINARY_DIR}" MATCHES "^${PROJECT_SOURCE_DIR}$")
ENDIF(VTK_WRAP_PYTHON)


